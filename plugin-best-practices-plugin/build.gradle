plugins {
  id 'java-gradle-plugin'
  id 'org.jetbrains.kotlin.jvm'
  id 'groovy'
  id 'maven-publish'
}

group = 'com.autonomousapps'
version = '0.1'

java {
  toolchain {
    languageVersion.set(JavaLanguageVersion.of(8))
  }
  withJavadocJar()
  withSourcesJar()
}

dependencies {
  implementation platform('org.jetbrains.kotlin:kotlin-bom')

  implementation 'org.jetbrains.kotlin:kotlin-stdlib-jdk8'
  implementation('com.autonomousapps:asm-relocated:9.2.0.2') {
    because 'Bytecode analysis'
  }
  implementation('com.google.guava:guava:31.1-jre') {
    because 'Graphs'
  }
}

testing {
  suites {
    // Configure the built-in test suite
    test {
      useJUnitJupiter()
      dependencies {
        implementation 'com.google.truth:truth:1.1.3'
      }
    }

    // Create a new test suite
    functionalTest(JvmTestSuite) {
      // Use Kotlin Test test framework
      useSpock()

      dependencies {
        implementation 'com.google.truth:truth:1.1.3'
      }

      targets {
        all {
          // This test suite should run after the built-in test suite has run its tests
          testTask.configure { shouldRunAfter(test) }
        }
      }
    }
  }
}

gradlePlugin {
  plugins {
    plugin {
      id = 'com.autonomousapps.plugin-best-practices-plugin'
      implementationClass = 'com.autonomousapps.GradleBestPracticesPlugin'
    }
  }
}

gradlePlugin.testSourceSets(sourceSets.functionalTest)

tasks.named('check') {
  dependsOn(testing.suites.functionalTest)
}
