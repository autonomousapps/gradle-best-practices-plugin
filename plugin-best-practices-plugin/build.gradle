plugins {
  id 'java-gradle-plugin'
  id 'org.jetbrains.kotlin.jvm'
  id 'org.jetbrains.kotlin.plugin.serialization'
  id 'groovy'
  id 'maven-publish'
  id 'com.gradle.plugin-publish'
  id 'com.autonomousapps.plugin-best-practices-plugin'
}

group = 'com.autonomousapps'
version = '0.8-SNAPSHOT'

java {
  toolchain {
    languageVersion.set(JavaLanguageVersion.of(11))
  }
  withJavadocJar()
  withSourcesJar()
}

dependencies {
  implementation platform('org.jetbrains.kotlin:kotlin-bom')

  implementation 'org.jetbrains.kotlin:kotlin-stdlib-jdk8'
  implementation('com.autonomousapps:asm-relocated:9.2.0.2') {
    because 'Bytecode analysis'
  }
  implementation('com.autonomousapps:graph-support:0.1') {
    because 'Graphs'
  }
  implementation('com.google.guava:guava:31.1-jre') {
    because 'Graphs'
  }
  implementation('org.jetbrains.kotlinx:kotlinx-serialization-json:1.3.3') {
    because 'Serialization support'
  }
}

testing {
  suites {
    // Configure the built-in test suite
    test {
      useJUnitJupiter()
      dependencies {
        implementation 'com.google.truth:truth:1.1.3'
      }
    }

    // Create a new test suite
    functionalTest(JvmTestSuite) {
      // Use Kotlin Test test framework
      useSpock()

      dependencies {
        implementation 'com.google.truth:truth:1.1.3'
      }

      targets {
        all {
          // This test suite should run after the built-in test suite has run its tests
          testTask.configure { shouldRunAfter(test) }
        }
      }
    }
  }
}

gradlePlugin {
  plugins {
    plugin {
      id = 'com.autonomousapps.plugin-best-practices-plugin'
      implementationClass = 'com.autonomousapps.GradleBestPracticesPlugin'
      displayName = 'Gradle Best Practices Plugin'
      description = 'Gradle Plugin that detects violations of Gradle best practices in Gradle Plugins'
    }
  }
}

gradlePlugin.testSourceSets(sourceSets.functionalTest)

def check = tasks.named('check') {
  dependsOn(testing.suites.functionalTest)
}

tasks.named('publishPlugins') {
  dependsOn(check)
}

pluginBundle {
  website = 'https://github.com/autonomousapps/gradle-best-practices-plugin'
  vcsUrl = 'https://github.com/autonomousapps/gradle-best-practices-plugin'
  tags = ['best practices']
}
